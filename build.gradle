plugins {
    id 'maven-publish'
    id 'dev.architectury.loom' version '0.10.0-SNAPSHOT'
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

apply from: 'https://files.latmod.com/public/markdown-git-changelog.gradle'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

def ENV = System.getenv()
version = "${mod_version}-build.${ENV.GITHUB_RUN_NUMBER ?: 9999}"
archivesBaseName = project.archives_base_name
group = project.maven_group

loom {
    silentMojangMappingsLicense()

    forge {
        mixinConfigs = [
        ]

        dataGen {
            mod project.mod_id
        }

        launches {
            data {
                arg "--existing", file("src/main/resources").absolutePath
            }
        }
    }
}

repositories {
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

    maven { url "https://maven.saps.dev/minecraft" }
    maven { url "https://maven.architectury.dev" }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    mappings minecraft.officialMojangMappings()
    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

    modApi "dev.ftb.mods:ftb-library-forge:$ftb_mods_lib2" transitive false
    modApi "dev.ftb.mods:ftb-chunks-forge:$ftb_mods_chunks" transitive false
    modApi "dev.ftb.mods:ftb-teams-forge:$ftb_mods_teams" transitive false

    modApi "dev.architectury:architectury-forge:$arch_version"
}

processResources {
    inputs.property "version", project.version
    inputs.property "teams_version", ftb_mods_teams

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version, "team_version": ftb_mods_teams
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = targetVersion
    }
}

java {
    // withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }

    manifest {
        attributes([
                "Specification-Title"     : project.mod_id,
                "Specification-Vendor"    : project.mod_author,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : project.mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    classifier "dev"
}

//jar.finalizedBy('reobfJar')
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
                artifactId = archivesBaseName
                classifier null
            }
        }
    }

    repositories {
        if (ENV.SAPS_TOKEN) {
            maven {
                url "https://maven.saps.dev/minecraft"
                credentials {
                    username = "ftb"
                    password = "${ENV.SAPS_TOKEN}"
                }
            }
        }

        if (ENV.FTB_MAVEN_TOKEN) {
            maven {
                url "https://maven.ftb.dev/release"
                credentials {
                    username = "ftb"
                    password = "${ENV.FTB_MAVEN_TOKEN}"
                }
            }
        }
    }
}

if (ENV.CURSEFORGE_KEY) {
    curseforge {
        apiKey = ENV.CURSEFORGE_KEY
        project {
            id = project.curseforge_id
            releaseType = "alpha"
            addGameVersion "Forge"
            addGameVersion "1.18.1"
            mainArtifact(remapJar.archivePath)
            changelog = getGitChangelog
            changelogType = 'markdown'
        }
    }
}
